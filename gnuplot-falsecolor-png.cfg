set datafile separator ","
set terminal unknown
set yrange [-1:1]

plot '< grep result infile.csv | sort -rnk2' using (0):(0):3:9 with circles lc palette fillstyle solid noborder

set terminal pngcairo enhanced font 'Arial,14' size 1280,1280 background rgb "black"
set encoding utf8
set xlabel 'Angle (deg)' tc rgb 'gray'
set ylabel 'Angle (deg)' tc rgb 'gray'
set border lc rgb 'gray'
#set palette defined (0 'black', 1 'white')
set palette defined (0 0 0 0, 0.0001 0.2 0.0 0.0, 0.001 0.2 0.4 0.0, 0.01 0.2 0.4 0.6, 0.1 0.8 0.8 0.6, 1 1 1 1)
set cblabel 'Directional gravitational field intensity (m * s^-2 * deg^-2)' tc rgb 'gray'
unset key
min = (GPVAL_DATA_X_MIN * 1.25)
max = (GPVAL_DATA_X_MAX * 1.25)
lbl = max * 0.95
lbl2 = max * 0.90
lbl3 = max * 0.93
reticle = (GPVAL_DATA_X_MAX * 1.00)
minc = GPVAL_DATA_CB_MIN
maxc = GPVAL_DATA_CB_MAX
maxc2 = (maxc * 1.1)
set xrange [min:max]
set yrange [min:max]
set cbrange [0:maxc]
set format cb '%1.1tx10^{%L}'
set object 10 circle at first 0,0 size first reticle fc rgb "#A00000" lw 1 dt "-  " front
set label 20 "Gravity Camera Simulation" at 0,lbl center front font 'Arial,24' tc rgb 'white'
set label 30 "kevinloch.com/gcsim" at 0,lbl2 center front font 'Arial,14' tc rgb 'white'
set label 40 "`grep Observing infile.csv | cut -f 2 -d ',' | cut -f 2-10 -d ' '`\n`grep Density infile.csv | cut -f 2 -d ',' | cut -f 2-10 -d ' '`\n`grep Altitude infile.csv | cut -f 2 -d ',' | cut -f 2-10 -d ' '`\n" at -lbl,lbl front tc rgb 'gray'
set label 50 "Imaging mode: Directional intensity\n`grep Sampling infile.csv | cut -f 2 -d ',' | cut -f 2-10 -d ' '`\n`grep resolution infile.csv | cut -f 2 -d "," | cut -f 2-10 -d ' '`" at lbl,lbl right front tc rgb 'gray'
set label 60 "Peak directional intensity: `grep 'result, 0,' infile.csv | cut -f 10 -d ','` m/s^2 * deg^2" at -lbl,-lbl3 left front tc rgb 'gray'
set label 70 "Total z-component intensity: `grep totalforcez infile.csv | cut -f 4 -d ',' | cut -f 3 -d ' '` m/s^2" at lbl,-lbl3 right front tc rgb 'gray'
set multiplot 
set size square 
set origin 0,0.08
set lmargin at screen 0.07
set rmargin at screen 0.86
replot
unset object 10
unset label 20
unset label 30
unset label 40
unset label 50
unset label 60
unset label 70
set key tc rgb 'gray'
set size nosquare 1,0.15
set origin 0,0
set lmargin at screen 0.07
set rmargin at screen 0.86
set yrange [0:maxc2]
set xlabel 'Angle (deg)'
set ylabel 'DGFI (m * s^-2 * deg^-2)'
set format y '%1.1tx10^{%L}'
plot '< grep result infile.csv | sort -rnk2' using 3:10 with lines lc rgb "#2020A0" title 'Directional intensity z-component', '< grep result infile.csv | sort -rnk2' using 3:9 with lines lc rgb "#20A020" title 'Directional intensity', '< grep result infile.csv infile.csv | sort -rnk2' using (-$3):10 with lines lc rgb "#2020A0" notitle, '< grep result infile.csv | sort -rnk2' using (-$3):9 with lines lc rgb "#20A020" notitle
unset multiplot
